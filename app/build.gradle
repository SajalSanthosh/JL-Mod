plugins {
    id 'com.android.application'
}

android {
    compileSdk COMPILE_SDK
    ndkVersion NDK_VERSION
    namespace 'ru.playsoftware.j2meloader'

    defaultConfig {
        applicationId "ru.woesss.j2meloader"
        minSdk MIN_SDK
        targetSdk TARGET_SDK
        versionCode 35
        versionName "0.83"
        resValue 'string', 'app_name', rootProject.name
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        viewBinding true
    }

    signingConfigs {
        emulator {
            def file = rootProject.file("keystore.properties")
            if (file.exists()) file.withInputStream {
                def keystoreProperties = new Properties()
                keystoreProperties.load(it)
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile rootProject.file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
        }
        debug {
            applicationIdSuffix '.debug'
            jniDebuggable true
            multiDexEnabled true
            multiDexKeepProguard file('multidex-config.pro')
        }
    }

    lint {
        disable 'MissingTranslation'
    }

    flavorDimensions 'default'
    productFlavors {
        emulator { // variant dimension for create emulator
            buildConfigField 'boolean', 'FULL_EMULATOR', 'true'
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.emulator
            versionNameSuffix System.getenv('VERSION_SUFFIX')
        }
        midlet { // variant dimension for create android port from J2ME app source
            buildConfigField 'boolean', 'FULL_EMULATOR', 'false'
            // configure midlet's port project params here, as default it read from app manifest,
            // placed to 'app/src/midlet/resources/MIDLET-META-INF/MANIFEST.MF'
            def props = getMidletManifestProperties()
            def midletName = props.getValue('MIDlet-Name').trim()
            def apkName = midletName.replaceAll('[/\\\\:*?\"<>|]', '').replace(' ', '_')
            applicationId "com.example.androidlet.${apkName.toLowerCase()}"
            versionName = props.getValue('MIDlet-Version')
            resValue 'string', 'app_name', midletName
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-midlet.pro'
        }
    }

    splits {
        abi {
            enable true
            include 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
            universalApk true
        }
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/cpp/Android.mk'
        }
    }

    applicationVariants.all {
        if (buildType.name == 'debug' && flavorName == 'emulator') {
            resValue 'string', 'app_name', 'JL-Debug'
        }
        outputs.all {
            outputFileName = "${rootProject.name}_${versionName}-${dirName}.apk".replaceAll('--+', '-')
        }
    }
}

def getMidletManifestProperties() {
    def mf = new java.util.jar.Manifest()
    def file = project.file('src/midlet/resources/MIDLET-META-INF/MANIFEST.MF')
    if (file.exists()) file.withInputStream { mf.read(it) }
    return mf.mainAttributes
}

dependencies {
    implementation project(':dexlib')

    def roomVersion = '2.5.1'
    annotationProcessor "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"

    annotationProcessor 'com.google.auto.service:auto-service:1.0.1'
    compileOnly 'com.google.auto.service:auto-service-annotations:1.0.1'

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'ch.acra:acra-dialog:5.9.7'
    implementation 'com.android.volley:volley:1.2.1'
    implementation 'com.github.billthefarmer:mididriver:v1.21'
    implementation 'com.github.nikita36078:filepicker:4.3.0'
    implementation 'com.github.nikita36078:mobile-ffmpeg:v4.3.2-compact'
    implementation 'com.github.nikita36078:pngj:2.2.3'
    implementation 'com.github.yukuku:ambilwarna:2.0.1'
    implementation 'com.github.penn5:donations:3.6.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    //noinspection GradleDependency
    implementation 'net.lingala.zip4j:zip4j:2.11.0' // next version adds unwanted case sensitivity

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
